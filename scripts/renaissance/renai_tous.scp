//DEFAULT : JOUEUR
[FUNCTION GET_ALLSTATS_STARTPOINTS]
RETURN <EVAL 30 + (0<TAG.RENAISSANCE> * 5)>


[FUNCTION RENAITRE]
EVENTS -e_anti_mark_recall
TAG.anti_mark_recall=0

REMOVE_MAUVAIS_TAGS

SET_TOP

TAG.VDICE
TAG.VDWRONG
TAG.VDECOV
TAG.VDEDES
TAG.VDEHYTH
TAG.VDEFIRE
TAG.VDEDESPISE

TAG.CADHUS
TAG.BERYAN
TAG.NIMS
TAG.HERAKES
TAG.SENGIR
TAG.brennen

TAG.VERIFQUETE3
TAG.VERIFQUETED4
TAG.DEMON1
TAG.DEMON2
TAG.DEMON3
TAG.DEMON4
TAG.RANDOMDRAGON
TAG.DRAGONNETD

TAG.BOGHARDANE
TAG.BOGHARDANC
TAG.BOGHARDANA
TAG.BOGHARDANT
TAG.ARCHIMEDE
TAG.LISTERINE
TAG.ANTABOLIS
TAG.QUETETITUS
TAG.STARBOLT
DESHABILLER
TITLE * Level 1 *
TAG.XP=0
TAG.MILLEXP=0
TAG.LEVEL=1
TAG.PTSSTAT=<EVAL <TAG.RENAISSANCE>> * 10
TAG.PTSSKILLS=<EVAL <TAG.RENAISSANCE>> * 10

HITS=<GET_ALLSTATS_STARTPOINTS>
STAMINA=<GET_ALLSTATS_STARTPOINTS>
MANA=<GET_ALLSTATS_STARTPOINTS>
TAG.FORPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.DEXPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.INTPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.MAGIEPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.TACTICSPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.FENCINGPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.MACEFIGHTINGPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.SWORDSMANSHIPPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.PARRYINGPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.HEALINGPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.WRESTLINGPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.MEDITATIONPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.MAGICRESISTANCEPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.ARCHERYPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.ANATOMYPURE=<GET_ALLSTATS_STARTPOINTS>
TAG.EvaluatingIntelPURE=<GET_ALLSTATS_STARTPOINTS>

GO Minoc
debogage_nosrc

[ITEMDEF i_dalle_anti_cheval1]
DEFNAME=i_dalle_anti_cheval1
NAME=Dalle anti cheval 1
ID=049b

ON=@CREATE
COLOR=0455
ATTR=08090

ON=@STEP
IF (<SRC.BODY>==c_man) || (<SRC.BODY>==c_woman) || (<SRC.BODY>==c_ghost_man) || (<SRC.BODY>==c_ghost_woman)
	RETURN 0
ELSE
	SRC.GO <SRC.HOME>
ENDIF

CATEGORY=Quetes
SUBSECTION=Quetes-seraph-2
DESCRIPTION=Dalle anti cheval 1

[ITEMDEF i_dalle_anti_cheval2]
DEFNAME=i_dalle_anti_cheval2
NAME=Dalle anti cheval 2
ID=049b

ON=@CREATE
COLOR=0455
ATTR=08090

ON=@STEP
IF <SRC.FINDLAYER.25>
	SRC.FINDLAYER.25.REMOVE
	SRC.UPDATE
ENDIF

CATEGORY=Quetes
SUBSECTION=Quetes-seraph-2
DESCRIPTION=Dalle anti cheval 2

[EVENTS e_anti_mark_recall]
ON=@SPELLCAST
IF ( <ARGN> == 22 ) || ( <ARGN> == 32 ) || ( <ARGN> == 45 ) || ( <ARGN> == 52 ) || ( <ARGN> == 40 ) || ( <ARGN> == 56 ) || ( <ARGN> == 44 )
SRC.SYSMESSAGEUA 0481 1 1 0 Vous ne pouvez pas lancer ce sortilege en ces lieux !
RETURN 1
ENDIF

ON=@SKILLSTART
IF (<SRC.ACTION>==Skill_Hiding) || (<SRC.ACTION>==SKILL_STEALTH)
SRC.SYSMESSAGEUA 0481 1 1 0 Vous ne pouvez pas effectuer cette action en ces lieux !
SRC.ACTION=-1
RETURN 1
ENDIF

[ITEMDEF i_dalle_teleportatrice]
DEFNAME=i_dalle_teleportatrice
NAME=Dalle teleportatrice
ID=049b

ON=@CREATE
COLOR=0ab8
ATTR=08090

ON=@STEP
IF (<SRC.OBODY> == c_man) || (<SRC.OBODY> == c_woman)
	SRC.P=<MOREP>
	SRC.UPDATE
ELSE
	RETURN 0
ENDIF

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> > 3
	MOREP=<SRC.P>
	SRC.SYSMESSAGEUA 0481 1 1 0 Coordonnee enregistree pour teleportation
	RETURN 1
ENDIF
RETURN 0

CATEGORY=Quetes
SUBSECTION=Quetes-seraph-1
DESCRIPTION=Dalle teleportatrice

[ITEMDEF m_effaceur_monstre_rebour]
ID=i_memory
TYPE=t_eq_script

ON=@CREATE
TIMER=120

ON=@TIMER
TOPOBJ.REMOVE
RETURN 1

//MORE1=1 MONTE  MORE1=2 DESCEND
//MORE2=NOMBRE RESTANT A MONTER OU DESCENDRE
//LINK.TAG.BOUGE = 1 SI LE MUR EST EN TRAIN DE BOUGER
[ITEMDEF i_dalle_monteuse_mur]
NAME=dalle
ID=049b
TYPE=t_normal

ON=@Create
COLOR=035c
ATTR=08010
MORE1=1
TIMER=-1
MORE2=10

ON=@STEP
IF 0<LINK.TAG.BOUGE> == 0
	LINK.TAG.BOUGE = 1
	TIMER=1
ENDIF
RETURN 1

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> > 3
	SRC.SYSMESSAGEUA 0481 1 1 0 Selectionner le mur que montera cette dalle...
	TARGET
ENDIF
RETURN 1

ON=@TIMER
LINK.TAG.BOUGE = 1
IF <MORE2>==0
	IF <MORE1>==1
		MORE1=2
		MORE2=10
		TIMER=20
	ELSE
		MORE1=1
		MORE2=10
		LINK.TAG.BOUGE
		TIMER=-1
	ENDIF
ELSE
	MORE2=<EVAL <MORE2> - 1>
	TIMER=1
	IF <MORE1>==1
		LINK.P=<EVAL <LINK.P.X>>,<EVAL <LINK.P.Y>>,<EVAL <LINK.P.Z> + 2>
		UPDATE
	ELSE
		LINK.P=<EVAL <LINK.P.X>>,<EVAL <LINK.P.Y>>,<EVAL <LINK.P.Z> - 2>
		UPDATE
	ENDIF
ENDIF
RETURN 1

ON=@TARGON_ITEM
SRC.SYSMESSAGEUA 0481 1 1 0 <SRC.TARG.NAME> a ete selectionne
LINK = <SRC.TARG.UID>
MORE1=1
TIMER=-1
MORE2=10
RETURN 1

//MORE1=1 TUE  MORE1=0 OK
//LINK = UID DE LA PROCHAINE DALLE A ACTIVER
[ITEMDEF i_grape_dalle_qui_tue]
NAME=dalle
ID=049b
TYPE=t_normal

ON=@Create
COLOR=035
ATTR=08010
MORE1=0
TIMER=-1

ON=@STEP
IF <MORE1> == 1 && !<SRC.BRAIN>
	SRC.KILL
ENDIF
RETURN 1

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> > 3
	SRC.SYSMESSAGEUA 0481 1 1 0 Selectionner la prochaine dalle a activer avec elle...
	TARGET
ENDIF
RETURN 1

ON=@TIMER
IF <MORE1>==0
	MORE1=1
	COLOR=020
ELSE
	MORE1=0
	COLOR=035
ENDIF
TIMER=-1
IF <LINK>
	IF <LINK.MORE1> == <MORE1>
		LINK.TIMER=-1
	ELSEIF <LINK.TIMER> == -1
		LINK.TIMER=1
	ENDIF
ENDIF
RETURN 1

ON=@TARGON_ITEM
IF <SRC.TARG.BASEID> == i_grape_dalle_qui_tue
	SRC.SYSMESSAGEUA 0481 1 1 0 <SRC.TARG.NAME> a ete selectionnee !
	LINK = <SRC.TARG.UID>
	SRC.TARG.MORE1 = <MORE1>
	TIMER=-1
	SRC.TARG.TIMER=-1
ELSE
	SRC.SYSMESSAGEUA 0481 1 1 0 <SRC.TARG.NAME> n'est pas une dalle du meme type !
ENDIF
RETURN 1

//MOREX = COMBIEN DE TEMPS AVANT CHANCE DE MODIFIER LA GRAPE
//MOREY = CHANCE EN POURCENT DE MODIFIER LA GRAPE AU TIMER=0
//MORE1 = UID DE LA PREMIERE DALLE DE LA GRAPE
//MORE2 = 1 : DOIT REPRENDRE SA FORME INITIAL AU TIMER
[ITEMDEF i_levier_grape_dalle_qui_tue]
NAME=Levier
ID=01095
TYPE=t_normal

ON=@Create
ATTR=08010
MOREX=0
MOREY=101
MORE1=0
TIMER=3000

ON=@DCLICK
IF (<MOREX> == 0) && (<SRC.ACCOUNT.PLEVEL> > 3)
	SRC.SYSMESSAGEUA 0481 1 1 0 Nombre de secondes avant de modifier la grape de dalles
	INPDLG MOREX 6
ELSEIF (<MOREY> > 100) && (<SRC.ACCOUNT.PLEVEL> > 3)
	SRC.SYSMESSAGEUA 0481 1 1 0 Chance en pourcent de modifier la grape apres le timer
	INPDLG MOREY 6
ELSEIF (<MORE1> == 0) && (<SRC.ACCOUNT.PLEVEL> > 3)
	SRC.SYSMESSAGEUA 0481 1 1 0 Selectionner la premiere dalle de la grape
	TARGET
ELSEIF (<MOREX> == 0) || (<MOREY> > 100) || (<MORE1> == 0)
	SRC.SYSMESSAGEUA 0481 1 1 0 Levier mal configure !
ELSE
	LINK = <MORE1>
	IF <LINK.BASEID> == i_grape_dalle_qui_tue
		IF <LINK.TIMER> == -1
			DISPID=01094
			UPDATE
			LINK.TIMER=1
			TIMER=2
			MORE2 = 1
		ELSE
			SRC.SYSMESSAGEUA 0481 1 1 0 Le levier est coince !
		ENDIF
	ELSE
		SRC.SYSMESSAGEUA 0481 1 1 0 Levier mal configure !
	ENDIF
	LINK=04fffffff
ENDIF
RETURN 1

ON=@TIMER
IF <MORE2> == 1
	DISPID = 01095
	UPDATE
ELSE
	IF <EVAL rand(100)> < <MOREY>
		LINK = <MORE1>
		IF <LINK> && <LINK.TIMER> == -1
			LINK.TIMER=1
		ENDIF
		LINK=04fffffff
	ENDIF
ENDIF
MORE2=0
IF <MOREX> > 0
	TIMER=<MOREX>
ELSE
	REMOVE
ENDIF
RETURN 1

ON=@TARGON_ITEM
IF <SRC.TARG.BASEID> == i_grape_dalle_qui_tue
	MORE1=<SRC.TARG.UID>
ELSE
	SRC.SYSMESSAGEUA 0481 1 1 0 Vous devez selectionner la premiere dalle qui tue
ENDIF
RETURN 1


[ITEMDEF i_vire_creature]
NAME=vire creature
ID=i_memory
TYPE=t_eq_script

ON=@CREATE
TIMER=120

ON=@TIMER
CONT.REMOVE
REMOVE
RETURN 1


[ITEMDEF i_dalle_teleport_tempon]
NAME=Dalle teleport tempon
ID=049b

ON=@CREATE
COLOR=0ab8
ATTR=08090

ON=@STEP
IF <SRC.FINDID.m_dalle_teleport_tempon>
	SRC.FINDID.m_dalle_teleport_tempon.MORE1 = 1
ENDIF
RETURN 1

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> > 3
	MOREP=<SRC.P>
	SRC.SYSMESSAGEUA 0481 1 1 0 Coordonnee enregistree pour teleportation
	RETURN 1
ENDIF
RETURN 0

//MEMORY NECESSAIRE POUR PASSER LA FIN
//MORE1=0 ==> OK ; MORE1=1 ==> TELEPORT
[ITEMDEF m_dalle_teleport_tempon] 
ID=i_memory
NAME=Tempon teleport
TYPE=t_eq_script 
LAYER=30 

ON=@CREATE 
TIMER=3600

ON=@TIMER 
REMOVE 
RETURN 1

[ITEMDEF i_dalle_teleport_tempon_debut]
NAME=Dalle teleport tempon debut
ID=049b

ON=@CREATE
COLOR=020
ATTR=08090

ON=@STEP
IF <SRC.FINDID.m_dalle_teleport_tempon>
	SRC.FINDID.m_dalle_teleport_tempon.MORE1 = 0
ELSE
	SRC.NEWITEM=m_dalle_teleport_tempon
	SRC.ACT.CONT = <SRC.UID>	
ENDIF
RETURN 1

[ITEMDEF i_dalle_teleport_tempon_fin]
NAME=Dalle teleport tempon fin
ID=049b

ON=@CREATE
COLOR=025
ATTR=08090

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> > 3
	MOREP=<SRC.P>
	SRC.SYSMESSAGEUA 0481 1 1 0 Coordonnee enregistree pour teleportation
	RETURN 1
ENDIF
RETURN 0

ON=@STEP
IF (<SRC.OBODY> == c_man) || (<SRC.OBODY> == c_woman)
	IF !((<SRC.FINDID.m_dalle_teleport_tempon>) && (<SRC.FINDID.m_dalle_teleport_tempon.MORE1> == 0))
		SRC.P=<MOREP>
		SRC.UPDATE
	ENDIF
ENDIF
RETURN 1