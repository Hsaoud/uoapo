[ITEMDEF i_dalle_piegee_d7_1]
NAME=Dalle piegee [Lave]
ID=049b

ON=@CREATE
COLOR=0bb1
ATTR=08090

ON=@STEP
IF !<SRC.BRAIN>
	SRC.NEWITEM i_effet_piege_laby_d7
	SRC.ACT.P=<EVAL <SRC.P.X>+1>,<EVAL <SRC.P.Y>+1>,<SRC.P.Z>
	SRC.DAMAGE {600 700} 0008
	IF !<SRC.FINDID.i_rune_curse_piege_d7>
		SRC.NEWITEM i_rune_curse_piege_d7
		SRC.ACT.LINK=<SRC.UID>
		SRC.ACT.EQUIP
		SRC.ACT.TIMER=240
	ENDIF
ENDIF

[ITEMDEF i_dalle_piegee_d7_2]
NAME=Dalle Correcte
ID=049b

ON=@CREATE
COLOR=041
ATTR=08090

ON=@STEP
IF !<SRC.BRAIN>
	SRC.NEWITEM i_effet_piege_laby_d7
	SRC.ACT.P=<EVAL <SRC.P.X>+1>,<EVAL <SRC.P.Y>+1>,<SRC.P.Z>
	SRC.DAMAGE 10 0008
ENDIF


//MORE1=DIRECTION DE LA PORTE ; MORE2=1(EN PLACE) 2(DÉPLACÉ) ; MOREX=nb de kills a avoir
[ITEMDEF i_dalle_leveur_d7]
NAME=Dalle leveur
ID=049b

ON=@CREATE
COLOR=058
ATTR=08090
MORE2=1

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> > 6
	IF (!STRCMPI("<LINK>","04fffffff")
		SRC.SYSMESSAGEUA 0481 1 1 0 Selectionnez l'objet a bouger
		TARGET
	ENDIF
	MORE1=<EVAL <MORE1> + 1>
	IF <EVAL <MORE1>> > 4
		MORE1=1
	ENDIF
	IF <EVAL <MORE1>> == 1
		SRC.SYSMESSAGEUA 0481 1 1 0 Direction nord
	ELSEIF <EVAL <MORE1>> == 2
		SRC.SYSMESSAGEUA 0481 1 1 0 Direction est
	ELSEIF <EVAL <MORE1>> == 3
		SRC.SYSMESSAGEUA 0481 1 1 0 Direction sud
	ELSE
		SRC.SYSMESSAGEUA 0481 1 1 0 Direction ouest
	ENDIF
ENDIF
RETURN 1

ON=@TARGON_ITEM
LINK=<SRC.TARG.UID>
RETURN 1

ON=@STEP
IF !<SRC.BRAIN>
	IF (STRCMPI("<LINK>","04fffffff"))
		IF <EVAL 0<SRC.TAG.EPREUVE_MONSTRE_MORT_D7>> >= <EVAL <MOREX>>
			IF <EVAL <MORE2>> == 1
				IF <MORE1> == 1
					LINK.P=<EVAL <LINK.P.X>>,<EVAL <LINK.P.Y> - 1>,<EVAL <LINK.P.Z>>
					LINK.UPDATE
				ELSEIF <MORE1> == 2
					LINK.P=<EVAL <LINK.P.X> + 1>,<EVAL <LINK.P.Y>>,<EVAL <LINK.P.Z>>
					LINK.UPDATE
				ELSEIF <MORE1> == 3
					LINK.P=<EVAL <LINK.P.X>>,<EVAL <LINK.P.Y> + 1>,<EVAL <LINK.P.Z>>
					LINK.UPDATE
				ELSE
					LINK.P=<EVAL <LINK.P.X> - 1>,<EVAL <LINK.P.Y>>,<EVAL <LINK.P.Z>>
					LINK.UPDATE
				ENDIF
				TIMER=5
				MORE2=2
				SRC.TAG.EPREUVE_MONSTRE_MORT_D7
			ENDIF
		ENDIF
	ENDIF
ENDIF
RETURN 1

ON=@TIMER
TIMER=-1
MORE2=1
IF <MORE1> == 1
	LINK.P=<EVAL <LINK.P.X>>,<EVAL <LINK.P.Y> + 1>,<EVAL <LINK.P.Z>>
	LINK.UPDATE
ELSEIF <MORE1> == 2
	LINK.P=<EVAL <LINK.P.X> - 1>,<EVAL <LINK.P.Y>>,<EVAL <LINK.P.Z>>
	LINK.UPDATE
ELSEIF <MORE1> == 3
	LINK.P=<EVAL <LINK.P.X>>,<EVAL <LINK.P.Y> - 1>,<EVAL <LINK.P.Z>>
	LINK.UPDATE
ELSE
	LINK.P=<EVAL <LINK.P.X> + 1>,<EVAL <LINK.P.Y>>,<EVAL <LINK.P.Z>>
	LINK.UPDATE
ENDIF
RETURN 1


//VERT=044(MORE1=1) JAUNE=035(MORE1=2) ROUGE=021(MORE1=3)
[ITEMDEF i_dalle_multi_d7]
NAME=Dalle piegee
ID=049b

ON=@CREATE
COLOR=044
MORE1=1
ATTR=08010

ON=@STEP
IF <EVAL <MORE1>> == 3
	SRC.TAG.EPREUVES_REUSSIES_D7
	SRC.TAG.EPREUVE_A_FAIRE_D7
	SRC.KILL
ENDIF
RETURN 1

ON=@TIMER
IF <EVAL <MORE1>> == 1
	MORE1=2
	COLOR=035
	TIMER=3
ELSEIF <EVAL <MORE1>> == 2
	MORE1=3
	COLOR=021
	TIMER=3
ELSE
	MORE1=1
	COLOR=044
	TIMER=<EVAL (rand(6)) + 3>
ENDIF
RETURN 1


//MORE2 = (0 AJUSTER LINK ; 1 AJUSTER MOREP) ; MORE1 = (0 LAISSER ; 1 TELEPORT)
[ITEMDEF i_dalle_teleport_double]
NAME=Dalle de teleport double
ID=049b

ON=@CREATE
COLOR=02f
ATTR=08090

ON=@STEP
IF !<SRC.BRAIN>
	IF <EVAL <MOREX>> > 0
		IF <MORE1> == 1
			SRC.GO <MOREP>
		ENDIF
		TIMER=3
	ENDIF
ENDIF
RETURN 1

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> > 3
	IF <MORE2> == 0
		SRC.SYSMESSAGEUA 0481 1 1 0 Selectionner l'autre dalle a lier
		TARGET
	ELSE
		MOREP=<SRC.P>
		SRC.SYSMESSAGEUA 0481 1 1 0 Coordonnee enregistree pour teleportation
	ENDIF
	RETURN 1
ENDIF
RETURN 0

ON=@TARGON_ITEM
IF <SRC.TARG.BASEID> == i_dalle_teleport_double
	MORE2=1
	MORE1=0
	SRC.TARG.MORE2=1
	SRC.TARG.MORE1=1
	LINK=<SRC.TARG.UID>
	SRC.TARG.LINK=<UID>
	SRC.SYSMESSAGEUA 0481 1 1 0 dalle liee correctement!
ELSE
	SRC.SYSMESSAGEUA 0481 1 1 0 Il faut lier avec une autre dalle de teleport double
ENDIF
RETURN 1

ON=@TIMER
TIMER=-1
IF <MORE1> == 1
	MORE1=0
	LINK.MORE1=1
ELSE
	MORE1=1
	LINK.MORE1=0
ENDIF
RETURN 1


//PLUS LE MORE1 EST ÉLEVÉ, PLUS LE JOUEUR EST LOIN DANS LE LABY (COMMENCE A 1)
[ITEMDEF i_dalle_de_message_d7]
NAME=Dalle de message
ID=049b

ON=@CREATE
COLOR=0bb6
ATTR=08090
MORE1=0

ON=@STEP
IF !<SRC.BRAIN>
	IF <EVAL 0<SRC.TAG.FIN_LABY_CHECKPOINT_D7>> < <EVAL <MORE1>>
		SRC.TAG.FIN_LABY_CHECKPOINT_D7=<MORE1>
		SRC.SFX 016e
		IF <EVAL <MORE1>> == 1
			SRC.SYSMESSAGEUA 025 1 1 0 Voix morbide : Bienvenue dans mon antre faible mortel!
		ELSEIF <EVAL <MORE1>> == 2
			SRC.SYSMESSAGEUA 025 1 1 0 Voix morbide : Votre heure approche!
		ELSEIF <EVAL <MORE1>> == 3
			SRC.SYSMESSAGEUA 025 1 1 0 Voix morbide : Vous pensez toujours que vous y arriverez?
		ELSEIF <EVAL <MORE1>> == 4
			SRC.SYSMESSAGEUA 025 1 1 0 Voix morbide : Abandonnez maintenant, vous n'avez encore rien vu!
		ELSEIF <EVAL <MORE1>> == 5
			SRC.SYSMESSAGEUA 025 1 1 0 Voix morbide : Vous n'avez aucune chance
		ELSEIF <EVAL <MORE1>> == 6
			SRC.SYSMESSAGEUA 025 1 1 0 Voix morbide : Ce n'est pas du courage, c'est de la folie!
		ELSEIF <EVAL <MORE1>> == 7
			SRC.SYSMESSAGEUA 025 1 1 0 Voix morbide : La mort n'est rien compare a ce qui vous attend!
		ELSEIF <EVAL <MORE1>> == 8
			SRC.SYSMESSAGEUA 025 1 1 0 Voix morbide : Votre coeur est la derniere chose que vous verrez avant de mourir!
		ELSEIF <EVAL <MORE1>> == 9
			SRC.SYSMESSAGEUA 025 1 1 0 Voix morbide : Votre tombe est deja creusee!
		ELSEIF <EVAL <MORE1>> == 10
			SRC.SYSMESSAGEUA 025 1 1 0 Voix morbide : Partez maintenant, ceci est mon dernier avertissement!
		ELSEIF <EVAL <MORE1>> == 11
			SRC.SYSMESSAGEUA 025 1 1 0 Voix morbide : Vous l'avez voulu, preparez vous a mourir!
		ENDIF
	ENDIF
	IF <MORE1> == 11
		SRC.GO 5536,883,30
	ENDIF
ENDIF
RETURN 1


//MORE1= SUITE A FAIRE PAR LE JOUEUR
//DALLES DU LABY EN ETOILE
[ITEMDEF i_dalle_passeur_fin_d7]
NAME=Dalle passeur d7
ID=049b

ON=@CREATE
COLOR=0b81
ATTR=08090

ON=@STEP
IF !<SRC.BRAIN>
	IF <EVAL 0<SRC.TAG.LABY_CARRE_D7>> == <EVAL <MORE1> - 1>
		SRC.TAG.LABY_CARRE_D7=<EVAL <SRC.TAG.LABY_CARRE_D7> + 1>
	ELSEIF <EVAL 0<SRC.TAG.LABY_CARRE_D7>> != <EVAL <MORE1>>
		SRC.TAG.LABY_CARRE_D7=0
	ENDIF
	IF <EVAL 0<SRC.TAG.LABY_CARRE_D7>> == 9
		SRC.TAG.LABY_CARRE_D7
		SRC.GO 5588,857,45
	ENDIF
ENDIF
RETURN 1


//MORE1=0 ==> PAS DE TELEPORT ; MORE1=1 ==> TELEPORT
//UTILE DANS LE LABY BIZARRE A LA FIN
[ITEMDEF i_dalle_teleport_temp]
NAME=Dalle teleport temp
ID=049b

ON=@CREATE
COLOR=053
ATTR=08090

ON=@STEP
IF !<SRC.BRAIN>
	IF <MORE1> == 1
		IF <EVAL <MOREX>> > 0
			SRC.P = <MOREP>
			SRC.UPDATE
		ENDIF
	ENDIF
ENDIF
RETURN 1

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> > 3
	MOREP=<SRC.P>
	SRC.SYSMESSAGEUA 0481 1 1 0 Coordonnee enregistree pour teleportation
	IF <MORE1> == 0
		MORE1 = 1
		COLOR = 0136
	ELSE
		MORE1 = 0
		COLOR = 053
	ENDIF
ENDIF
RETURN 1


//MORE1 = temps dans les airs en secondes ; MORE2=direction
[ITEMDEF i_dalle_leveur_d_objet]
NAME=Dalle
ID=049b

ON=@CREATE
COLOR=0483
ATTR=08010

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> > 3
	IF (!STRCMPI("<LINK>","04fffffff")
		SRC.SYSMESSAGEUA 0481 1 1 0 Selectionnez l'objet a bouger
		TARGET
	ELSEIF <MORE1>==0
		SRC.SYSMESSAGEUA 0481 1 1 0 Temps en secondes avant que l'objet revienne a sa hauteur normale
		INPDLG MORE1 3
	ELSE
		MORE2=<EVAL <MORE2> + 1>
		IF <EVAL <MORE2>> > 4
			MORE2=2
		ENDIF
		IF <EVAL <MORE2>> == 1
			SRC.SYSMESSAGEUA 0481 1 1 0 Direction nord
		ELSEIF <EVAL <MORE2>> == 2
			SRC.SYSMESSAGEUA 0481 1 1 0 Direction est
		ELSEIF <EVAL <MORE2>> == 3
			SRC.SYSMESSAGEUA 0481 1 1 0 Direction sud
		ELSE
			SRC.SYSMESSAGEUA 0481 1 1 0 Direction ouest
		ENDIF
	ENDIF
ENDIF
RETURN 1

ON=@TARGON_ITEM
IF <SRC.TARG.ATTR> == 08010
	LINK=<SRC.TARG.UID>
ELSE
	SRC.SYSMESSAGEUA 0481 1 1 0 Il faut lier un objet bloque au sol
ENDIF
RETURN 1

ON=@STEP
IF !<SRC.BRAIN>
	IF ( (STRCMPI("<LINK>","04fffffff")) && (<EVAL <MORE1>> > 0) && (<EVAL <MORE2>> > 0))
		IF <LINK.ATTR>==08010
			IF <TIMER> == -1
				IF <MORE2> == 1
					LINK.P=<EVAL <LINK.P.X>>,<EVAL <LINK.P.Y> - 1>,<EVAL <LINK.P.Z>>
					LINK.UPDATE
				ELSEIF <MORE2> == 2
					LINK.P=<EVAL <LINK.P.X> + 1>,<EVAL <LINK.P.Y>>,<EVAL <LINK.P.Z>>
					LINK.UPDATE
				ELSEIF <MORE2> == 3
					LINK.P=<EVAL <LINK.P.X>>,<EVAL <LINK.P.Y> + 1>,<EVAL <LINK.P.Z>>
					LINK.UPDATE
				ELSE
					LINK.P=<EVAL <LINK.P.X> - 1>,<EVAL <LINK.P.Y>>,<EVAL <LINK.P.Z>>
					LINK.UPDATE
				ENDIF
				TIMER=<MORE1>
				COLOR=0b78
			ENDIF
		ELSE
			SRC.SYSMESSAGEUA 0481 1 1 0 L'objet doit etre bloque au sol
		ENDIF
	ENDIF
ENDIF
RETURN 1

ON=@TIMER
TIMER=-1
COLOR=0483
IF <MORE2> == 1
	LINK.P=<EVAL <LINK.P.X>>,<EVAL <LINK.P.Y> + 1>,<EVAL <LINK.P.Z>>
	LINK.UPDATE
ELSEIF <MORE2> == 2
	LINK.P=<EVAL <LINK.P.X> - 1>,<EVAL <LINK.P.Y>>,<EVAL <LINK.P.Z>>
	LINK.UPDATE
ELSEIF <MORE2> == 3
	LINK.P=<EVAL <LINK.P.X>>,<EVAL <LINK.P.Y> - 1>,<EVAL <LINK.P.Z>>
	LINK.UPDATE
ELSE
	LINK.P=<EVAL <LINK.P.X> + 1>,<EVAL <LINK.P.Y>>,<EVAL <LINK.P.Z>>
	LINK.UPDATE
ENDIF
RETURN 1


[ITEMDEF i_dalle_teleport_simple]
NAME=Dalle de teleport simple
ID=049b

ON=@CREATE
COLOR=0b10
ATTR=08090

ON=@STEP
IF !<SRC.BRAIN>
	SRC.GO <MOREP>
ENDIF
RETURN 1

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> > 6
	MOREP=<SRC.P>
	SRC.SYSMESSAGEUA 0481 1 1 0 Coordonnee enregistree pour teleportation
	RETURN 1
ENDIF
RETURN 0


//PLUS LE MORE1 EST ÉLEVÉ, PLUS LE JOUEUR EST LOIN DANS LE LABY (COMMENCE A 1)
[ITEMDEF i_dalle_inside_d7]
NAME=Dalle
ID=049b

ON=@CREATE
COLOR=0b97
ATTR=08090
MORE1=0

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> > 6
	MORE1=<EVAL <MORE1> + 1>
	SRC.SYSMESSAGEUA 0481 1 1 0 Checkpoint numero <EVAL <MORE1>>
	RETURN 1
ENDIF
RETURN 0

ON=@STEP
IF !<SRC.BRAIN>
	VAR.SUA_COLOR 025
	IF <EVAL 0<SRC.TAG.INSIDE_D7>> == <EVAL <MORE1> - 1>
		SRC.TAG.INSIDE_D7=<MORE1>
	ENDIF
ENDIF
RETURN 1


//MORE1 : # dans l'ordre que doit etre rendu le joueur
[ITEMDEF i_dalle_detect_tricheur_d7]
NAME=Dalle
ID=049b

ON=@CREATE
COLOR=0b9e
ATTR=08090

ON=@STEP
IF !<SRC.BRAIN>
	IF <SRC.ACCOUNT.PLEVEL> < 6
		IF <SRC.FINDLAYER.25>
			SRC.FINDLAYER.25.REMOVE
			SRC.UPDATE
			UID.09903F.NEWITEM 0bd0
			UID.09903F.ACT.NAME voir tags
			UID.09903F.ACT.TAG.RTIME=<SERV.RTIME>
			UID.09903F.ACT.TAG.PERSO_ACCOUNT=<SRC.NAME> acc:<SRC.ACCOUNT>
			UID.09903F.ACT.TAG.POSITION=<SRC.P>
			UID.09903F.ACT.TAG.RAISON=cheval
			UID.09903F.ACT.COLOR=025
			UID.09903F.ACT.ATTR=08010
			UID.09903F.ACT.CONT=0b6a09
		ENDIF
		IF <EVAL 0<SRC.TAG.INSIDE_D7>> >= <EVAL <MORE1>>
			RETURN 1
		ELSE
			//TRICHEUR
			IF <EVAL 0<SRC.TAG.INSIDE_D7> + 2> <= <EVAL <MORE1>>
				UID.09903F.NEWITEM 0bd0
				UID.09903F.ACT.NAME voir tags
				UID.09903F.ACT.TAG.RTIME=<SERV.RTIME>
				UID.09903F.ACT.TAG.PERSO_ACCOUNT=<SRC.NAME> acc:<SRC.ACCOUNT>
				UID.09903F.ACT.TAG.POSITION=<SRC.P>
				UID.09903F.ACT.TAG.ETAITMORE=<SRC.TAG.INSIDE_D7>
				UID.09903F.ACT.TAG.DEVIENTMORE=<MORE1>
				UID.09903F.ACT.COLOR=025
				UID.09903F.ACT.ATTR=08010
				UID.09903F.ACT.CONT=0b6a09
			ENDIF
			SRC.TAG.INSIDE_D7=<MORE1>
		ENDIF
	ENDIF
ENDIF
RETURN 1

ON=@TIMER
TIMER=-1
RETURN 1