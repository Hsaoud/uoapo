[PLEVEL 1]
tax

//PERMET A UN MEMBRE DE LA GUILDE DE LA GARDE DE DONNER UNE CONTRAVENTION A UN FAUTIF
[FUNCTION tax]
IF (STRCMPI("<GUILD_VILLE>","0"))
	NEWITEM i_selectionneur_tax
	ACT.EQUIP
	ACT.USE
ENDIF
RETURN 0


//DEFAULT=i_plaque_entree_maison
[FUNCTION SET_MAIRE_BULLETIN]
IF ((<UID.<VAR.UID_PLAQUE_MAIRE>.BASEID> == i_plaque_mairie) && (<BASEID> == i_vote_mairie))
	TAG.CANDIDAT1=<UID.<VAR.UID_PLAQUE_MAIRE>.TAG.CANDIDAT1>
	TAG.CANDIDAT2=<UID.<VAR.UID_PLAQUE_MAIRE>.TAG.CANDIDAT2>
	TAG.CANDIDAT3=<UID.<VAR.UID_PLAQUE_MAIRE>.TAG.CANDIDAT3>
	TAG.CANDIDAT4=<UID.<VAR.UID_PLAQUE_MAIRE>.TAG.CANDIDAT4>
	TAG.CANDIDAT5=<UID.<VAR.UID_PLAQUE_MAIRE>.TAG.CANDIDAT5>
	TAG.CANDIDAT6=<UID.<VAR.UID_PLAQUE_MAIRE>.TAG.CANDIDAT6>
ENDIF


//ENTREE : VAR.VILLE ; VAR.REGION
//SORTIE : 1 ==> LE MAIRE PEUT BLOQUER OU GARDE TAXER
[FUNCTION IS_REGION_DANS_VILLE]
IF (!STRCMPI("<VAR.VILLE>","Britain"))
	IF (!STRCMPI("<VAR.REGION>","Britain"))
		RETURN 1
	ENDIF
ELSEIF (!STRCMPI("<VAR.VILLE>","Cove"))
	IF (!STRCMPI("<VAR.REGION>","Cove"))
		RETURN 1
	ENDIF
ELSEIF (!STRCMPI("<VAR.VILLE>","Minoc"))
	IF (!STRCMPI("<VAR.REGION>","Minoc_centre") || !STRCMPI("<VAR.REGION>","Minoc") || !STRCMPI("<VAR.REGION>","MAISON_MINOC19"))
		RETURN 1
	ENDIF
ELSEIF (!STRCMPI("<VAR.VILLE>","Nujel'm"))
	IF (!STRCMPI("<VAR.REGION>","Nujel'm"))
		RETURN 1
	ENDIF
ELSEIF (!STRCMPI("<VAR.VILLE>","Skara Brae"))
	IF (!STRCMPI("<VAR.REGION>","Skara Brae"))
		RETURN 1
	ENDIF
ELSEIF (!STRCMPI("<VAR.VILLE>","Vesper"))
	IF (!STRCMPI("<VAR.REGION>","Vesper"))
		RETURN 1
	ENDIF
ELSEIF (!STRCMPI("<VAR.VILLE>","Yew"))
	IF (!STRCMPI("<VAR.REGION>","Yew"))
		RETURN 1
	ENDIF
ENDIF
RETURN 0


//RETURN LE UID DE LA PLAQUE DE MAIRIE
[FUNCTION GET_UID_PLAQUE_MAIRIE]
IF (!STRCMPI("<TAG.VILLE>","Britain") )
	RETURN 0
ELSEIF (!STRCMPI("<TAG.VILLE>","Cove") )
	RETURN 0
ELSEIF (!STRCMPI("<TAG.VILLE>","Minoc") )
	RETURN 04001870e
ELSEIF (!STRCMPI("<TAG.VILLE>","Nujel'm") )
	RETURN 0
ELSEIF (!STRCMPI("<TAG.VILLE>","Skara Brae") )
	RETURN 0
ELSEIF (!STRCMPI("<TAG.VILLE>","Vesper") )
	RETURN 0
ELSEIF (!STRCMPI("<TAG.VILLE>","Yew") )
	RETURN 0
ELSE
	RETURN 0
ENDIF



[FUNCTION RESET_PLAQUE_ENTREE]
UPDATE_PAYER_PLAQUE <EVAL 86400 - <TIMER>>
TIMER=86400
TAG.JOURS_INACTIFS
IF <TAG.UID_PARCHO_SAISI>
	UID.09903F.ACT = <TAG.UID_PARCHO_SAISI>
	IF <UID.09903F.ACT.BASEID> == i_scroll_blank
		UID.09903F.ACT.REMOVE
	ENDIF
	TAG.UID_PARCHO_SAISI
ENDIF
RETURN 1


//DEFAULT : i_plaque_entree_maison
[FUNCTION MESSAGE_PLAQUE_ENTREE]
IF <EVAL 0<TAG.A_VENDRE>> == 0
	IF <EVAL 0<TAG.VALEUR_VENTE_JOUEUR>> > 0
		MESSAGE <LINK.NAME> vend cette maison <EVAL <TAG.VALEUR_VENTE_JOUEUR>> pieces d'or (double click pour acheter)
	ELSE
		MESSAGE Plaque exterieure de <LINK.NAME>
	ENDIF
ELSE
	MESSAGE Maison a vendre : <EVAL 0<TAG.DETTE>> pieces d'or
ENDIF
RETURN 1


//SI DEFAULT A UNE GUILDE VERIFIER QUE C'EST LE GARDE
[FUNCTION GUILD_VILLE]
FORCHARLAYER 30
	IF <LINK.BASEID> == i_guildstone
		IF <LINK.UID> == 040030c1a
			RETURN Minoc
		ELSEIF <LINK.UID> == 04003c491
			RETURN Nujel'm
		ELSE
			RETURN 0
		ENDIF
	ENDIF
ENDFOR
RETURN 0

//INCREMENTE LE TEMPS QUE LA MAISON EST NON PAYEE OU EFFACER SI PAYEE
//REVENDRE LA MAISON SI 30 JOURS SONT ECOULES ET AVERTIR QUAND IL RESTE 7 JOURS
//ARGS = TIMER A INCREMENTER ; DEFAULT = PLAQUE
[FUNCTION UPDATE_PAYER_PLAQUE]
IF 0<TAG.DETTE> == 0
	SUPPRIMER_PARCHO_NP
	RETURN 1
ENDIF
TAG.SECONDES_NP = <EVAL 0<TAG.SECONDES_NP> + <ARGS>>
IF <TAG.SECONDES_NP> >= 86400
	TAG.SECONDES_NP = <EVAL <TAG.SECONDES_NP> - 86400>
	TAG.JOURS_NP = <EVAL 0<TAG.JOURS_NP> + 1>
	IF <TAG.JOURS_NP> >= 23
		IF <TAG.JOURS_NP> == 29
			VAR.TEMP = ""
		ELSE
			VAR.TEMP = s
		ENDIF
		IF <TAG.UID_PARCHO_NP>
			UID.09903F.ACT = <TAG.UID_PARCHO_NP>
			IF <UID.09903F.ACT.BASEID> == i_scroll_blank
				UID.09903F.ACT.NAME = Il vous reste moins de <EVAL 30 - <TAG.JOURS_NP>> jour<VAR.TEMP> pour payer votre demeure !
			ELSE
				TAG.UID_PARCHO_NP
			ENDIF
		ELSE
			UID.09903F.NEWITEM i_scroll_blank
			UID.09903F.ACT.COLOR = 0bb6
			UID.09903F.ACT.ATTR = 08010
			UID.09903F.ACT.P = <P>
			UID.09903F.ACT.NAME = Il vous reste moins de <EVAL 30 - <TAG.JOURS_NP>> jour<VAR.TEMP> pour payer votre demeure !
			TAG.UID_PARCHO_NP = <UID.09903F.ACT.UID>
			UID.09903F.ACT.UPDATE
		ENDIF
		IF <TAG.JOURS_NP> >= 30
			UID.09903F.ACT = <TAG.UID_PARCHO_NP>
			UID.09903F.ACT.NAME = Votre demeure sera saisie des qu'un maitre de jeu se connectera !
			IF 0<UID.09903F.ACT.TAG.DEMANDE_MAISON_SAISIE> == 0
				UID.09903F.ACT.TAG.DEMANDE_MAISON_SAISIE = 1
				UID.09903F.NEWITEM i_scroll_blank
				UID.09903F.ACT.NAME La maison de <LINK.NAME> situee a <P> n'a pas ete payee et doit etre revendue
				UID.09903F.ACT.COLOR=025
				UID.09903F.ACT.ATTR=08010
				UID.09903F.ACT.P=5451,1107
				UID.09903F.ACT.UPDATE
				TIMER=-1
			ENDIF
		ENDIF
	ENDIF
ENDIF
RETURN 1

//ARGS : NB A MONTER
//SRC : JOUEUR QUI VEUT MONTER
//DEFAULT : OBJET A MONTER
[FUNCTION PEUT_MONTER_OBJET]
IF <EVAL 0<ARGS>> >= 0
	//ON MONTE
	IF <EVAL <P.Z> + <ARGS>> < <EVAL <SRC.P.Z> + 20>
		RETURN 1
	ELSE
		SRC.SYSMESSAGEUA 025 1 1 0 Vous ne pouvez pas monter <NAME> de <EVAL <ARGS>> unites !
	ENDIF
ELSE IF <EVAL 0<ARGS>> < 0
	//ON DESCEND
	IF <EVAL <P.Z> + <ARGS>> > <EVAL <SRC.P.Z>>
		RETURN 1
	ELSE
		SRC.SYSMESSAGEUA 025 1 1 0 Vous ne pouvez pas descendre <NAME> de <EVAL <ARGS>> unites !
	ENDIF
ELSE
	SRC.SYSMESSAGEUA 025 1 1 0 Erreur : ARGS n'est pas un nombre : (<ARGS>)
ENDIF
RETURN 0


//DEFAULT : L'OBJET
//ARGS : NOM DE L'ENDROIT(REGION) DE LA PLAQUE
//SRC : JOUEUR
[FUNCTION PEUT_BOUGER_OBJET]
IF <TOPOBJ.UID> != <SRC.UID>
	IF <OBJECT_INSIDE_HOME <ARGS>>
		IF ( (<SRC.TARG.baseid> == i_plaque_de_proprietaire) || (<SRC.TARG.baseid> == i_plaque_entree_maison) )
			SRC.SYSMESSAGEUA 025 1 1 0 Impossible de bouger ou modifier les attributs d'une plaque !
		ELSE
			IF <SRC.UID> == <LINK.UID>
				RETURN 1
			ELSE
				SRC.SYSMESSAGEUA 025 1 1 0 Vous n'etes pas le propretaire de <NAME> !
			ENDIF
		ENDIF
	ELSE
		SRC.SYSMESSAGEUA 025 1 1 0 <NAME> n'est pas dans la meme region que votre plaque !
	ENDIF
ELSE
	SRC.SYSMESSAGEUA 025 1 1 0 Impossible de bouger ou modifier les attributs d'un objet sur vous !
ENDIF
RETURN 0


//DEFAULT : L'OBJET
//ARGS : NOM DE L'ENDROIT(REGION) DE LA PLAQUE
[FUNCTION OBJECT_INSIDE_HOME]
IF (STRCMPI("<ARGS>",""))
	IF !(STRCMPI("<ARGS>","<REGION.NAME>"))
		RETURN 1
	ENDIF
ENDIF
RETURN 0


//DEFAULT = PLAQUE
[FUNCTION SUPPRIMER_PARCHO_NP]
IF <TAG.UID_PARCHO_NP>
	UID.09903F.ACT = <TAG.UID_PARCHO_NP>
	IF <UID.09903F.ACT.BASEID> == i_scroll_blank
		UID.09903F.ACT.REMOVE
	ENDIF
	TAG.UID_PARCHO_NP
ENDIF