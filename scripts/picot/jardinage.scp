//MORE1L = NB DE FRUITS DANS LA PLANTE [0]
//MORE1H = MAX DE FRUITS DANS LA PLANTE [0-255]
//MORE2L = NB DE MINUTES AVANT D'AVOIR UNE POUSSE [1-65000]
//MORE2H = NB DE MINUTES AVANT D'AVOIR UNE PLANTE [1-65000]
//MOREX = libre
//MOREY = NB DE MINUTES AVANT D'AVOIR UN FRUIT SUPPLEMENTAIRE [1-65000]
//MOREZ = 1/MOREZ QUE LA RECOLTE DETRUIRA LA PLANTE [1, 2, ..., 255]
//TAG.BASEID_FRUIT
//TAG.DISPID_PLANTE
//TAG.DISPID_POUSSE
[TYPEDEF t_plante_1]
ON=@CREATE
TIMER=10

ON=@CLICK
IF <MORE2H> == 0//PLANTE
	IF <MORE1L> < <EVAL <MORE1H>/5>
		MESSAGE <NAME>[Vide]
	ELSEIF <MORE1L> < <EVAL (<MORE1H>*2)/5>
		MESSAGE <NAME>[Degarni]
	ELSEIF <MORE1L> < <EVAL (<MORE1H>*3)/5>
		MESSAGE <NAME>[Peu garni]
	ELSEIF <MORE1L> < <EVAL (<MORE1H>*4)/5>
		MESSAGE <NAME>[Tres garni]
	ELSE
		MESSAGE <NAME>[Plein]
	ENDIF
ELSEIF <MORE2L> == 0//POUSSE
	MESSAGE Pousse
ELSE//GRAINE DANS LA TERRE
	MESSAGE Terre
ENDIF
RETURN 1

ON=@DCLICK
IF <MORE2H> == 0//PLANTE
	IF ((<MORE1L> == 0) || (0<TAG.BASEID_FRUIT> == 0))
		MESSAGE <NAME>
	ELSE
		IF <EVAL RAND(<MOREZ>)> == 0
			SRC.SYSMESSAGEUA 0481 1 1 0 Vous brisez <NAME> en recoltant les fruits !
			MORE2H = <TAG.INIT_MORE2H>
			MORE2L = <TAG.INIT_MORE2L>
			TIMER=1
			DISPID=031f5
			UPDATE
		ENDIF
		SRC.NEWITEM <TAG.BASEID_FRUIT>
		SRC.ACT.AMOUNT=<MORE1L>
		SRC.ACT.CONT = <SRC.UID>
		MORE1L = 0
	ENDIF
ELSEIF <MORE2L> == 0//POUSSE
	MESSAGE Pousse
ELSE//GRAINE DANS LA TERRE
	MESSAGE Terre
ENDIF
RETURN 1

ON=@TIMER
IF 0<TAG.INIT_MORE2H> == 0
	IF <MORE2H> == 0
		MORE2H = 60
	ENDIF
	TAG.INIT_MORE2H = <MORE2H>
	IF <MORE2L> == 0
		MORE2L = 60
	ENDIF
	TAG.INIT_MORE2L = <MORE2L>
	IF <MOREY> == 0
		MOREY = 60
	ENDIF
	TAG.INIT_MOREY = <MOREY>
ENDIF
IF <MORE2H> == 0//PLANTE
	MOREY -= 1
	IF <MOREY> == 0
		MORE1L += 1
		IF <MORE1L> > <MORE1H>
			MORE1L = <MORE1H>
		ENDIF
		MOREY = <TAG.INIT_MOREY>
	ENDIF
ELSEIF <MORE2L> == 0//POUSSE
	MORE2H -= 1
	IF <MORE2H> == 0
		MORE1L = 0
		DISPID=<TAG.DISPID_PLANTE>
		UPDATE
	ENDIF
ELSE//GRAINE DANS LA TERRE
	MORE2L -= 1
	IF <MORE2L> == 0
		DISPID=<TAG.DISPID_POUSSE>
		UPDATE
	ENDIF
ENDIF
TIMER=60
RETURN 1

[ITEMDEF i_plante_piment_doux]
NAME=Plante de piment doux
ID=031f5
TYPE=t_plante_1
WEIGHT=1

ON=@CREATE
TAG.BASEID_FRUIT=i_piment_doux
MORE1L=0
MORE2H=120
MORE2L=60
MORE1H=10
TAG.DISPID_POUSSE=018e2
TAG.DISPID_PLANTE=0c93
MOREZ=5
MOREY=60

[ITEMDEF i_plante_piment_jalapeno]
NAME=plante de piment jalapeno
ID=031f5
TYPE=t_plante_1
WEIGHT=1

ON=@CREATE
TAG.BASEID_FRUIT=i_piment_jalapeno
MORE1L=0
MORE2H=180
MORE2L=90
MORE1H=6
TAG.DISPID_POUSSE=018e2
TAG.DISPID_PLANTE=0c93
MOREZ=5
MOREY=60

[ITEMDEF i_plante_piment_habanero]
NAME=plante de piment habanero
ID=031f5
TYPE=t_plante_1
WEIGHT=1

ON=@CREATE
TAG.BASEID_FRUIT=i_piment_habanero
MORE1L=0
MORE2H=360
MORE2L=180
MORE1H=2
TAG.DISPID_POUSSE=018e2
TAG.DISPID_PLANTE=0c93
MOREZ=5
MOREY=60


[ITEMDEF i_plante_de_garlic]
NAME=Plante de garlic
ID=031f5
TYPE=t_plante_1
WEIGHT=1

ON=@Create
TAG.BASEID_FRUIT=i_reag_garlic
MORE1L=0
MORE2H=20
MORE2L=20
MORE1H=25
TAG.DISPID_POUSSE=01aa2
TAG.DISPID_PLANTE=031f5
MOREZ=1
MOREY=20

CATEGORY=Genese
SUBSECTION=Plante reactifs
DESCRIPTION=Garlic


[ITEMDEF i_plante_de_nightshade]
NAME=Plante de nightshade
ID=031f5
TYPE=t_plante_1
WEIGHT=1

ON=@Create
TAG.BASEID_FRUIT=i_reag_nightshade
MORE1L=0
MORE2H=20
MORE2L=20
MORE1H=25
TAG.DISPID_POUSSE=01aa2
TAG.DISPID_PLANTE=018e6
MOREZ=1
MOREY=20

CATEGORY=Genese
SUBSECTION=Plante reactifs
DESCRIPTION=Nightshade


[ITEMDEF i_plante_de_mandrake]
NAME=Plante de mandrake
ID=031f5
TYPE=t_plante_1
WEIGHT=1

ON=@Create
TAG.BASEID_FRUIT=i_reag_mandrake_root
MORE1L=0
MORE2H=20
MORE2L=20
MORE1H=25
TAG.DISPID_POUSSE=01aa2
TAG.DISPID_PLANTE=018e0
MOREZ=1
MOREY=20

CATEGORY=Genese
SUBSECTION=Plante reactifs
DESCRIPTION=Mandrake

[ITEMDEF i_plante_de_ginseng]
NAME=Plante de ginseng
ID=031f5
TYPE=t_plante_1
WEIGHT=1

ON=@Create
TAG.BASEID_FRUIT=i_reag_ginseng
MORE1L=0
MORE2H=20
MORE2L=20
MORE1H=25
TAG.DISPID_POUSSE=01aa2
TAG.DISPID_PLANTE=018ea
MOREZ=1
MOREY=20

CATEGORY=Genese
SUBSECTION=Plante reactifs
DESCRIPTION=Ginseng
