[PLEVEL 2]
testmacrotage

[FUNCTION TESTMACROTAGE]
NEWITEM i_selectionneur_test_macrotage
ACT.EQUIP
ACT.USE
RETURN 1

[ITEMDEF i_selectionneur_test_macrotage] 
NAME=selection macro
ID=i_memory
TYPE=t_eq_script 
LAYER=30

ON=@CREATE
TIMER=30

ON=@DCLICK
SRC.SYSMESSAGEUA 0481 1 1 0 Selectionnez le joueur a controler le macrotage...
TARGET
RETURN 1

ON=@TARGON_ITEM
SRC.SYSMESSAGEUA 0481 1 1 0 <SRC.TARG.NAME> n'est pas un joueur !
REMOVE
RETURN 1

ON=@TARGON_CHAR
IF !<SRC.TARG.BRAIN>
	SRC.SYSMESSAGEUA 0481 1 1 0 <SRC.TARG.NAME> a maintenant 45 secondes pour repondre !
	SERV.LOG <SRC.NAME> lance un test macrotage a <SRC.TARG.NAME> ACC: <SRC.TARG.ACCOUNT>
	SRC.TARG.CREER_TEST_MACROTAGE <SRC.UID>
ELSE
	SRC.SYSMESSAGEUA 0481 1 1 0 <SRC.TARG.NAME> n'est pas un joueur !
ENDIF
REMOVE
RETURN 1

ON=@TIMER
REMOVE
RETURN 1

//LINK = MODERATEUR AYANT LANCE LE TEST
//TAG.BONNE_REPONSE ==> BONNE REPONSE
//TAG.REPONSE1, TAG.REPONSE2... TAG.REPONSE5 => CHOIX DE REPONSE
//TAG.QUESTION => QUESTION A POSER
[ITEMDEF m_test_macrotage]
NAME=testeur macrotage
ID=i_memory
TYPE=t_eq_script 
LAYER=30

ON=@CREATE
TIMER=45

ON=@TIMER
LINK.SYSMESSAGEUA 0481 1 1 0 <CONT.NAME> n'a pas repondu au test de macrotage !
SERV.LOG <CONT.NAME> ACC:<CONT.ACCOUNT> n'a pas repondu au test de macrotage !
REMOVE
RETURN 1

//LINK = MODERATEUR AYANT LANCE LE TEST
[ITEMDEF m_activate_test_macrotage]
NAME=activateur test macrotage
ID=i_memory
TYPE=t_eq_script 
LAYER=30

ON=@CREATE
TIMER=1

ON=@UNEQUIP
VAR.ACTSAVE = <SRC.ACT.UID>
SRC.NEWITEM m_test_macrotage
SRC.ACT.LINK = <LINK>
SRC.ACT.CONT = <SRC.UID>
SRC.ACT.GENERER_QUESTION_MACROTAGE
SRC.ACT = <VAR.ACTSAVE>
VAR.ACTSAVE
SRC.DIALOG D_TEST_MACROTAGE

ON=@TIMER
UNEQUIP
REMOVE
RETURN 1

[DIALOG D_TEST_MACROTAGE]
0,0

page 0
resizepic 100 100 2620 500 400

page 1
text 140 120 1152 1//CECI EST UNE TOURNEE DE MACROTAGE
text 120 140 1152 2//Veuillez repondre a la question suivante :
text 120 160 1152 3//<TAG.QUESTION>
text 140 180 1152 4//<TAG.REPONSE1>
text 140 200 1152 5//<TAG.REPONSE2>
text 140 220 1152 6//<TAG.REPONSE3>
text 140 240 1152 7//<TAG.REPONSE4>
text 140 260 1152 8//<TAG.REPONSE5>
button 120 185 2103 2104 1 0 1//REPONSE 1
button 120 205 2103 2104 1 0 2//REPONSE 2
button 120 225 2103 2104 1 0 3//REPONSE 3
button 120 245 2103 2104 1 0 4//REPONSE 4
button 120 265 2103 2104 1 0 5//REPONSE 5


[DIALOG D_TEST_MACROTAGE TEXT]
<VAR.BLANKLINE>
CECI EST UNE TOURNEE DE MACROTAGE
Veuillez repondre a la question suivante
<FINDID.m_test_macrotage.TAG.QUESTION>
<FINDID.m_test_macrotage.TAG.REPONSE1>
<FINDID.m_test_macrotage.TAG.REPONSE2>
<FINDID.m_test_macrotage.TAG.REPONSE3>
<FINDID.m_test_macrotage.TAG.REPONSE4>
<FINDID.m_test_macrotage.TAG.REPONSE5>

[DIALOG D_TEST_MACROTAGE BUTTON]
ON=0
DIALOG D_TEST_MACROTAGE
RETURN 1

ON=1
IF <FINDID.m_test_macrotage>
	IF 0<FINDID.m_test_macrotage.TAG.BONNE_REPONSE> == 1
		CHOIX_BONNE_REPONSE
	ELSE
		CHOIX_MAUVAISE_REPONSE
	ENDIF
ENDIF
RETURN 1

ON=2
IF <FINDID.m_test_macrotage>
	IF 0<FINDID.m_test_macrotage.TAG.BONNE_REPONSE> == 2
		CHOIX_BONNE_REPONSE
	ELSE
		CHOIX_MAUVAISE_REPONSE
	ENDIF
ENDIF
RETURN 1

ON=3
IF <FINDID.m_test_macrotage>
	IF 0<FINDID.m_test_macrotage.TAG.BONNE_REPONSE> == 3
		CHOIX_BONNE_REPONSE
	ELSE
		CHOIX_MAUVAISE_REPONSE
	ENDIF
ENDIF
RETURN 1

ON=4
IF <FINDID.m_test_macrotage>
	IF 0<FINDID.m_test_macrotage.TAG.BONNE_REPONSE> == 4
		CHOIX_BONNE_REPONSE
	ELSE
		CHOIX_MAUVAISE_REPONSE
	ENDIF
ENDIF
RETURN 1

ON=5
IF <FINDID.m_test_macrotage>
	IF 0<FINDID.m_test_macrotage.TAG.BONNE_REPONSE> == 5
		CHOIX_BONNE_REPONSE
	ELSE
		CHOIX_MAUVAISE_REPONSE
	ENDIF
ENDIF
RETURN 1

//********************************************
//****************FONCTIONS*******************
//********************************************

//DEFAULT = JOUEUR
//<ARGS> = UID DU MODERATEUR
[FUNCTION CREER_TEST_MACROTAGE]
IF (STRCMPI("<ARGS>",""))
	VAR.ACTSAVE = <ACT.UID>
	NEWITEM m_activate_test_macrotage
	ACT.LINK = <ARGS>
	ACT.CONT = <UID>
	ACT = <VAR.ACTSAVE>
	VAR.ACTSAVE
ENDIF
RETURN 1

//DEFAULT = JOUEUR
[FUNCTION CHOIX_BONNE_REPONSE]
FINDID.m_test_macrotage.LINK.SYSMESSAGEUA 0481 1 1 0 <NAME> reussi le test !
SERV.LOG <NAME> ACC:<ACCOUNT> a reussi le test
FINDID.m_test_macrotage.REMOVE
RETURN 1

//DEFAULT = JOUEUR
[FUNCTION CHOIX_MAUVAISE_REPONSE]
FINDID.m_test_macrotage.LINK.SYSMESSAGEUA 0481 1 1 0 <NAME> a donne une mauvaise reponse au test de macrotage !
SERV.LOG <NAME> ACC:<ACCOUNT> a echoue le test
FINDID.m_test_macrotage.REMOVE
RETURN 1

//DEFAULT = m_test_macrotage
[FUNCTION GENERER_QUESTION_MACROTAGE]
IF <BASEID> == m_test_macrotage
	VAR.TEMP = <EVAL RAND(2) + 1>
	IF <VAR.TEMP> == 1
		GQM_MATHEMATIQUE
	ELSEIF <VAR.TEMP> == 2
		GQM_FRANCAIS
	ENDIF
ENDIF
RETURN 1

//DEFAULT = m_test_macrotage
[FUNCTION GQM_INT_TO_STRING]
TAG.REPONSE1 = "<EVAL <TAG.REPONSE1>>"
TAG.REPONSE2 = "<EVAL <TAG.REPONSE2>>"
TAG.REPONSE3 = "<EVAL <TAG.REPONSE3>>"
TAG.REPONSE4 = "<EVAL <TAG.REPONSE4>>"
TAG.REPONSE5 = "<EVAL <TAG.REPONSE5>>"
RETURN 1

//***********************
//********SHUFFLE********
//***********************

//DEFAULT = m_test_macrotage
[FUNCTION SHUFFLE_TEST_MACROTAGE]
VAR.TEMPWHILE = 0
WHILE (<VAR.TEMPWHILE> < 10)
	VAR.TEMPWHILE = <EVAL <VAR.TEMPWHILE> + 1>
	VAR.TEMP1 = <EVAL RAND(5) + 1>
	VAR.TEMP2 = <EVAL RAND(5) + 1>
	IF <TAG.BONNE_REPONSE> == <VAR.TEMP1>
		TAG.BONNE_REPONSE = <VAR.TEMP2>
	ELSEIF <TAG.BONNE_REPONSE> == <VAR.TEMP2>
		TAG.BONNE_REPONSE = <VAR.TEMP1>
	ENDIF
	IF <VAR.TEMP1> == 1
		VAR.TEMP = <TAG.REPONSE1>
		IF <VAR.TEMP2> == 2
			TAG.REPONSE1 = <TAG.REPONSE2>
			TAG.REPONSE2 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 3
			TAG.REPONSE1 = <TAG.REPONSE3>
			TAG.REPONSE3 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 4
			TAG.REPONSE1 = <TAG.REPONSE4>
			TAG.REPONSE4 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 5
			TAG.REPONSE1 = <TAG.REPONSE5>
			TAG.REPONSE5 = <VAR.TEMP>
		ENDIF
	ELSEIF <VAR.TEMP1> == 2
		VAR.TEMP = <TAG.REPONSE2>
		IF <VAR.TEMP2> == 1
			TAG.REPONSE2 = <TAG.REPONSE1>
			TAG.REPONSE1 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 3
			TAG.REPONSE2 = <TAG.REPONSE3>
			TAG.REPONSE3 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 4
			TAG.REPONSE2 = <TAG.REPONSE4>
			TAG.REPONSE4 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 5
			TAG.REPONSE2 = <TAG.REPONSE5>
			TAG.REPONSE5 = <VAR.TEMP>
		ENDIF
	ELSEIF <VAR.TEMP1> == 3
		VAR.TEMP = <TAG.REPONSE3>
		IF <VAR.TEMP2> == 1
			TAG.REPONSE3 = <TAG.REPONSE1>
			TAG.REPONSE1 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 2
			TAG.REPONSE3 = <TAG.REPONSE2>
			TAG.REPONSE2 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 4
			TAG.REPONSE3 = <TAG.REPONSE4>
			TAG.REPONSE4 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 5
			TAG.REPONSE3 = <TAG.REPONSE5>
			TAG.REPONSE5 = <VAR.TEMP>
		ENDIF
	ELSEIF <VAR.TEMP1> == 4
		VAR.TEMP = <TAG.REPONSE4>
		IF <VAR.TEMP2> == 1
			TAG.REPONSE4 = <TAG.REPONSE1>
			TAG.REPONSE1 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 2
			TAG.REPONSE4 = <TAG.REPONSE2>
			TAG.REPONSE2 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 3
			TAG.REPONSE4 = <TAG.REPONSE3>
			TAG.REPONSE3 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 5
			TAG.REPONSE4 = <TAG.REPONSE5>
			TAG.REPONSE5 = <VAR.TEMP>
		ENDIF
	ELSEIF <VAR.TEMP1> == 5
		VAR.TEMP = <TAG.REPONSE5>
		IF <VAR.TEMP2> == 1
			TAG.REPONSE5 = <TAG.REPONSE1>
			TAG.REPONSE1 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 2
			TAG.REPONSE5 = <TAG.REPONSE2>
			TAG.REPONSE2 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 3
			TAG.REPONSE5 = <TAG.REPONSE3>
			TAG.REPONSE3 = <VAR.TEMP>
		ELSEIF <VAR.TEMP2> == 4
			TAG.REPONSE5 = <TAG.REPONSE4>
			TAG.REPONSE4 = <VAR.TEMP>
		ENDIF
	ENDIF
ENDWHILE
VAR.TEMPWHILE
VAR.TEMP1
VAR.TEMP2
VAR.TEMP
RETURN 1


//*******************************
//*********MATHEMATIQUE**********
//*******************************

//DEFAULT = m_test_macrotage
[FUNCTION GQM_MATHEMATIQUE]
VAR.TEMP = <EVAL RAND(3) + 1>
IF <VAR.TEMP> == 1
	GQM_MATHEMATIQUE_01
ELSEIF <VAR.TEMP> == 2
	GQM_MATHEMATIQUE_02
ELSEIF <VAR.TEMP> == 3
	GQM_MATHEMATIQUE_03
ENDIF
GQM_INT_TO_STRING
VAR.TEMP
RETURN 1

//DEFAULT = m_test_macrotage
//ADDITION
[FUNCTION GQM_MATHEMATIQUE_01]
VAR.TEMP1 = <EVAL RAND(50)>
VAR.TEMP2 = <EVAL RAND(50)>
TAG.QUESTION = Combien font <EVAL <VAR.TEMP1>> + <EVAL <VAR.TEMP2>> ?
TAG.BONNE_REPONSE = 1
TAG.REPONSE1 = <EVAL <VAR.TEMP1> + <VAR.TEMP2>>
WHILE (0<TAG.REPONSE2> == 0) || (0<TAG.REPONSE2> == <TAG.REPONSE1>)
	TAG.REPONSE2 = <EVAL RAND(100)>
ENDWHILE
WHILE (0<TAG.REPONSE3> == 0) || (0<TAG.REPONSE3> == <TAG.REPONSE1>)
	TAG.REPONSE3 = <EVAL RAND(100)>
ENDWHILE
WHILE (0<TAG.REPONSE4> == 0) || (0<TAG.REPONSE4> == <TAG.REPONSE1>)
	TAG.REPONSE4 = <EVAL RAND(100)>
ENDWHILE
WHILE (0<TAG.REPONSE5> == 0) || (0<TAG.REPONSE5> == <TAG.REPONSE1>)
	TAG.REPONSE5 = <EVAL RAND(100)>
ENDWHILE
SHUFFLE_TEST_MACROTAGE
VAR.TEMP1
VAR.TEMP2
RETURN 1

//DEFAULT = m_test_macrotage
//MULTIPLICATION
[FUNCTION GQM_MATHEMATIQUE_02]
VAR.TEMP1 = <EVAL RAND(12) + 1>
VAR.TEMP2 = <EVAL RAND(12) + 1>
TAG.QUESTION = Combien font <EVAL <VAR.TEMP1>> X <EVAL <VAR.TEMP2>> ?
TAG.BONNE_REPONSE = 1
TAG.REPONSE1 = <EVAL <VAR.TEMP1> * <VAR.TEMP2>>
WHILE (0<TAG.REPONSE2> == 0) || (0<TAG.REPONSE2> == <TAG.REPONSE1>)
	TAG.REPONSE2 = <EVAL RAND(144) + 1>
ENDWHILE
WHILE (0<TAG.REPONSE3> == 0) || (0<TAG.REPONSE3> == <TAG.REPONSE1>)
	TAG.REPONSE3 = <EVAL RAND(144) + 1>
ENDWHILE
WHILE (0<TAG.REPONSE4> == 0) || (0<TAG.REPONSE4> == <TAG.REPONSE1>)
	TAG.REPONSE4 = <EVAL RAND(144) + 1>
ENDWHILE
WHILE (0<TAG.REPONSE5> == 0) || (0<TAG.REPONSE5> == <TAG.REPONSE1>)
	TAG.REPONSE5 = <EVAL RAND(144) + 1>
ENDWHILE
SHUFFLE_TEST_MACROTAGE
VAR.TEMP1
VAR.TEMP2
RETURN 1

//DEFAULT = m_test_macrotage
//SOUSTRACTION
[FUNCTION GQM_MATHEMATIQUE_03]
VAR.TEMP1 = <EVAL RAND(50)>
VAR.TEMP2 = <EVAL RAND(50)>
IF <VAR.TEMP1> < <VAR.TEMP2>
	VAR.TEMP3 = <VAR.TEMP1>
	VAR.TEMP1 = <VAR.TEMP2>
	VAR.TEMP2 = <VAR.TEMP3>
	VAR.TEMP3
ENDIF
TAG.QUESTION = Combien font <EVAL <VAR.TEMP1>> - <EVAL <VAR.TEMP2>> ?
TAG.BONNE_REPONSE = 1
TAG.REPONSE1 = <EVAL <VAR.TEMP1> - <VAR.TEMP2>>
WHILE (0<TAG.REPONSE2> == <TAG.REPONSE1>)
	TAG.REPONSE2 = <EVAL RAND(50)>
ENDWHILE
WHILE (0<TAG.REPONSE3> == <TAG.REPONSE1>)
	TAG.REPONSE3 = <EVAL RAND(50)>
ENDWHILE
WHILE (0<TAG.REPONSE4> == <TAG.REPONSE1>)
	TAG.REPONSE4 = <EVAL RAND(50)>
ENDWHILE
WHILE (0<TAG.REPONSE5> == <TAG.REPONSE1>)
	TAG.REPONSE5 = <EVAL RAND(50)>
ENDWHILE
SHUFFLE_TEST_MACROTAGE
VAR.TEMP1
VAR.TEMP2
RETURN 1


//*******************************
//***********FRANCAIS************
//*******************************

//DEFAULT = m_test_macrotage
[FUNCTION GQM_FRANCAIS]
VAR.TEMP = <EVAL RAND(4) + 1>
IF <VAR.TEMP> == 1
	GQM_FRANCAIS_01
ELSEIF <VAR.TEMP> == 2
	GQM_FRANCAIS_02
ELSEIF <VAR.TEMP> == 3
	GQM_FRANCAIS_03
ELSEIF <VAR.TEMP> == 4
	GQM_FRANCAIS_04
ENDIF
VAR.TEMP
RETURN 1

//DEFAULT = m_test_macrotage
[FUNCTION GQM_FRANCAIS_01]
TAG.QUESTION = Quelle est la seule phrase SANS faute dans cette liste?
TAG.BONNE_REPONSE = 1
TAG.REPONSE1 = La pomme pousse.
TAG.REPONSE2 = Ses toi.
TAG.REPONSE3 = La bule tombe.
TAG.REPONSE4 = La bierre coules.
TAG.REPONSE5 = Le sizau coupe
SHUFFLE_TEST_MACROTAGE
RETURN 1

//DEFAULT = m_test_macrotage
[FUNCTION GQM_FRANCAIS_02]
TAG.QUESTION = Quel mot n'est PAS un verbe dans cette liste?
TAG.BONNE_REPONSE = 1
TAG.REPONSE1 = Tulipe
TAG.REPONSE2 = Manger
TAG.REPONSE3 = Tomber
TAG.REPONSE4 = Agir
TAG.REPONSE5 = Battre
SHUFFLE_TEST_MACROTAGE
RETURN 1

//DEFAULT = m_test_macrotage
[FUNCTION GQM_FRANCAIS_03]
TAG.QUESTION = Quel mot n'a pas la lettre h dans cette liste?
TAG.BONNE_REPONSE = 1
TAG.REPONSE1 = Poule
TAG.REPONSE2 = Mouche
TAG.REPONSE3 = Macher
TAG.REPONSE4 = Poche
TAG.REPONSE5 = Loche
SHUFFLE_TEST_MACROTAGE
RETURN 1

//DEFAULT = m_test_macrotage
[FUNCTION GQM_FRANCAIS_04]
TAG.QUESTION = Quel mot est au pluriel dans cette liste?
TAG.BONNE_REPONSE = 1
TAG.REPONSE1 = Nuages
TAG.REPONSE2 = Bonbon
TAG.REPONSE3 = Humeur
TAG.REPONSE4 = Jeu
TAG.REPONSE5 = Nu
SHUFFLE_TEST_MACROTAGE
RETURN 1