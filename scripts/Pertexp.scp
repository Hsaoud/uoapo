[ITEMDEF i_Pierre_de_zone_mort]
ID=0edd
NAME=[ Ne pas toucher ] Changeur de Regionflag
TYPE=t_script

ON=@CREATE
TIMER=300
ATTR=08090

ON=@TIMER
REGION.FLAGS 0
TIMER=300
RETURN 1

[ITEMDEF i_tombe]
DEFNAME=i_tombe
ID=01183
TYPE=T_SCRIPT
NAME=Tombe

ON=@CREATE
TIMER=10

ON=@TIMER
REMOVE
RETURN 1

[ITEMDEF i_dalle_teleportatrice_mort1]
DEFNAME=i_dalle_teleportatrice_mort1
NAME=Dalle teleportatrice de mort1
ID=049b

ON=@CREATE
COLOR=083ea
ATTR=08090

ON=@STEP
IF !((<SRC.BODY> == c_man) || (<SRC.BODY> == c_woman))
	SRC.RESURRECT
	SRC.SYSMESSAGEUA 0481 1 1 0 Apres une longue perte de connaissance vous vous reveillez au royaume des morts ...
	IF !(<SRC.FINDID.I_RAPATRIEUR_CORPS>)
		SRC.STR=9999
		SRC.NEWITEM I_RAPATRIEUR_CORPS
		SRC.TAG.IDENTIFIANT_RAPATRIEUR=<SRC.ACT.UID>
		SRC.ACT.BOUNCE
		SRC.STR=<EVAL <SRC.TAG.FORPURE>>
	ENDIF
ELSE
	RETURN 0
ENDIF

[ITEMDEF i_dalle_teleportatrice_mort2]
DEFNAME=i_dalle_teleportatrice_mort2
NAME=Dalle teleportatrice de mort 2
ID=049b

ON=@CREATE
COLOR=083ea
ATTR=08090

ON=@STEP
IF (<SRC.BODY> == c_man) || (<SRC.BODY> == c_woman)
	SRC.P=<MOREP>
	SRC.UPDATE
ENDIF
RETURN 0

[EVENTS e_pertexp]
ON=@CLICK
IF <ACTION> == 076
	VAR.ACTION_J = [Afk]
ENDIF
IF <KARMA> >= 0
	VAR.COULEUR_J = 0c7
ELSE
	VAR.COULEUR_J = 026
ENDIF
IF ( (<SRC.ACCOUNT.PLEVEL> > 3) && ((<SRC.ALLSHOW>) || (<SRC.DEBUG>)) )
	VAR.ALLSHOW_J = [<ACTION>]
ENDIF
IF <ACCOUNT.JAIL> == 1
	VAR.PRISON_J = [Prisonnier]
ENDIF
FORCHARLAYER 30
	IF <LINK.BASEID> == i_guildstone
		IF (STRCMPI("<LINK.ABBREV>",""))
			VAR.GUILD_J = [<LINK.ABBREV>]
		ENDIF
		VAR.UID_GUILDE = <LINK.UID>
	ENDIF
ENDFOR
IF ( (<SRC.UID> != <UID>) && (<SRC.IsMemeGuilde> == 1) )
	VAR.COULEUR_J = 044
ENDIF
IF ( (<SRC.ACCOUNT.PLEVEL> > 3) && (<FLAGS>&statf_hidden) )
	MESSAGE @<VAR.COULEUR_J> <NAME><VAR.ACTION_J><VAR.GUILD_J><VAR.ALLSHOW_J><VAR.PRISON_J>(STEALTH)
ELSE
	MESSAGE @<VAR.COULEUR_J> <NAME><VAR.ACTION_J><VAR.GUILD_J><VAR.ALLSHOW_J><VAR.PRISON_J>
ENDIF
VAR.COULEUR_J
VAR.GUILD_J
VAR.ALLSHOW_J
VAR.ACTION_J
VAR.UID_GUILDE
VAR.PRISON_J
RETURN 1

ON=@GETHIT
//SRC = attaquant
//SRC.ACT = victime
IF (<SRC.ACT.ISCHAR> && <SRC.ISCHAR>)
	IF (0<SRC.TAG.PROPRIO_UID> || (!<SRC.BRAIN>))
		IF ((<SRC.ACT.REGION.FLAGS>&08000) || (<SRC.REGION.FLAGS>&08000))
			IF !<SRC.BRAIN>
				SRC.SYSMESSAGEUA 0481 1 1 0 Vous ne pouvez pas effectuer cette action en ces lieux !
			ENDIF
			EFFECT 3,i_fx_glow,0,15
			RETURN 1
		ELSEIF (<SRC.ACT.FINDID.m_anti_rez_kill> || <SRC.FINDID.m_anti_rez_kill>)
			IF (!(<SRC.BRAIN>) && (0<SRC.TAG.DEBUTANT> == 1))
				SRC.REZ <SRC.ACT.NAME>
				RETURN 1
			ENDIF
		ENDIF
	ENDIF
ENDIF
RETURN 0

ON=@DEATH
//SRC = victime
//SRC.ACT = attaquant
IF <EVAL 0<SRC.ACT.TAG.PROPRIO_UID>>
	SRC.ACT=<SRC.ACT.TAG.PROPRIO_UID>
ENDIF
IF ((<SRC.ACT.BRAIN>) || (0<SRC.ACT.TAG.DEBUTANT> == 0))
	IF !(0<SRC.TAG.XP>==0)
		SRC.TAG.XP=<EVAL (<SRC.TAG.XP>/4) * 3>
	ENDIF
	IF <EVAL 0<SRC.TAG.LEVEL>> <89
		IF <EVAL <SRC.TAG.LEVEL>> <10
			IF <EVAL <SRC.TAG.LEVEL>>==1
				SRC.TAG.XPAVANTLEVEL= <EVAL (3000 - <EVAL <SRC.TAG.XP>>)>
			ELSEIF <EVAL <SRC.TAG.LEVEL>>==2
				SRC.TAG.XPAVANTLEVEL= <EVAL (3500 - <EVAL <SRC.TAG.XP>>)>
			ELSEIF <EVAL <SRC.TAG.LEVEL>>==3
				SRC.TAG.XPAVANTLEVEL= <EVAL (4000 - <EVAL <SRC.TAG.XP>>)>
			ELSEIF <EVAL <SRC.TAG.LEVEL>>==4
				SRC.TAG.XPAVANTLEVEL= <EVAL (4500 - <EVAL <SRC.TAG.XP>>)>
			ELSEIF <EVAL <SRC.TAG.LEVEL>>==5
				SRC.TAG.XPAVANTLEVEL= <EVAL (5000 - <EVAL <SRC.TAG.XP>>)>
			ELSEIF <EVAL <SRC.TAG.LEVEL>>==6
				SRC.TAG.XPAVANTLEVEL= <EVAL (5500 - <EVAL <SRC.TAG.XP>>)>
			ELSEIF <EVAL <SRC.TAG.LEVEL>>==7
				SRC.TAG.XPAVANTLEVEL= <EVAL (6000 - <EVAL <SRC.TAG.XP>>)>
			ELSEIF <EVAL <SRC.TAG.LEVEL>>==8
				SRC.TAG.XPAVANTLEVEL= <EVAL (6500 - <EVAL <SRC.TAG.XP>>)>
			ELSE
				SRC.TAG.XPAVANTLEVEL= <EVAL (7000 - <EVAL <SRC.TAG.XP>>)>
			ENDIF
		ENDIF
	ELSE
		IF !(<SRC.TAG.MILLEXP>==0)
			SRC.TAG.MILLEXP=<EVAL (<SRC.TAG.MILLEXP>/4) * 3>
		ENDIF
	ENDIF
ELSE
	IF !<SRC.FINDID.m_anti_rez_kill>
		SRC.NEWITEM m_anti_rez_kill
		SRC.ACT.CONT = <SRC.UID>
	ENDIF
ENDIF

ON=@DEATHCORPSE
RESURRECT

EFFECT 2,i_fx_explode,0,26
NEWITEM i_tombe
ACT.COLOR=<COLOR>
ACT.ATTR=00012
ACT.NAME Tombe de <NAME>
ACT.P=<P>
ENLEVE_OBJETS_D7

//SI LE JOUEUR EST DANS L'ARENE, IGNORER LE SCRIPT DE DROP
IF <EVAL 0<TAG.ENTREE_ARENE>> == 0
	NEWITEM i_flesh_head
	ACT.ATTR=00002
	ACT.LINK=<UID>
	ACT.NAME=Tete de <NAME>
	ACT.P=<P>
	ACT.EMOTE tomber au sol

	IF <EVAL RAND(5)> == 0

		VAR.TEMP = <EVAL RAND(25)>
		VAR.DROPID = 0

		IF <VAR.TEMP> == 0
			IF <FINDTYPE.T_ARMOR>
				VAR.DROPID = <FINDTYPE.T_ARMOR.UID> 
			ENDIF
		ELSEIF <VAR.TEMP> == 1
			IF <FINDTYPE.T_WEAPON_SWORD>
				VAR.DROPID = <FINDTYPE.T_WEAPON_SWORD.UID>
			ENDIF
		ELSEIF <VAR.TEMP> == 2
			IF <FINDTYPE.T_WEAPON_FENCE>
				VAR.DROPID = <FINDTYPE.T_WEAPON_FENCE.UID>
			ENDIF
		ELSEIF <VAR.TEMP> == 3
			IF <FINDTYPE.T_WEAPON_MACE_SMITH>
				VAR.DROPID = <FINDTYPE.T_WEAPON_MACE_SMITH.UID>
			ENDIF
		ELSEIF <VAR.TEMP> == 4
			IF <FINDTYPE.T_WEAPON_BOW>
				VAR.DROPID = <FINDTYPE.T_WEAPON_BOW.UID>
			ENDIF
		ELSEIF <VAR.TEMP> == 5
			IF <FINDTYPE.T_CLOTHING>
				VAR.DROPID = <FINDTYPE.T_CLOTHING.UID>
			ENDIF
		ELSE
			VAR.TOTAL_CASH = <EVAL (<RESCOUNT i_piece_diamant>*1000) + <RESCOUNT i_gold>> //COMBIEN LE JRS A DE GOLD
			VAR.RANDOM_CASH = <EVAL RAND(<EVAL <VAR.TOTAL_CASH>>)>
			IF <EVAL <VAR.RANDOM_CASH>> >= 10000
				VAR.RANDOM_CASH = <EVAL <VAR.RANDOM_CASH>/1000>
				VAR.RANDOM_CASH = <EVAL <VAR.RANDOM_CASH>*1000>
				PAYUP <VAR.RANDOM_CASH>
				NEWITEM i_piece_diamant
				ACT.AMOUNT = <EVAL <VAR.RANDOM_CASH>/1000>
				ACT.P = <P>	
				ACT.TIMER=80000
				ACT.UPDATE
			ELSEIF <EVAL <VAR.RANDOM_CASH>> > 0
				PAYUP <VAR.RANDOM_CASH>
				NEWITEM i_gold
				ACT.AMOUNT = <VAR.RANDOM_CASH>
				ACT.P = <P>
				ACT.TIMER=80000
				ACT.UPDATE
			ENDIF
		ENDIF

		IF <EVAL RAND(5)> == 0
			IF <VAR.DROPID> == 0
				IF <FINDTYPE.T_CLOTHING>
					VAR.DROPID = <FINDTYPE.T_CLOTHING.UID>
				ENDIF
			ENDIF
		ENDIF
		IF <VAR.DROPID> != 0
			TRYP 0 UID.<VAR.DROPID>.UNEQUIP
			TRYP 0 UID.<VAR.DROPID>.P = <P>
		ENDIF
		VAR.DROPID
		VAR.TEMP
		VAR.TOTAL_CASH
		VAR.RANDOM_CASH
	ENDIF
ELSE
	ACT.EMOTE "mourir"
ENDIF

EVENTS -e_anti_mark_recall
TAG.anti_mark_recall=0

TAG.INSIDE_D1
TAG.INSIDE_D2
TAG.INSIDE_D3
TAG.INSIDE_D4
TAG.INSIDE_D5
TAG.INSIDE_D6
TAG.INSIDE_D7
TAG.INSIDE_DH
TAG.ENTREE_ARENE
TAG.INSIDE_Q_LEGEND
TAG.LOGOUTGREENACRES

P=2458,518,16
UPDATE

//DEFAULT = JOUEUR ; VAR.UID_GUILDE = GUILDE DE L'AUTRE JOUEUR
[FUNCTION IsMemeGuilde]
FORCHARLAYER 30
	IF <LINK.BASEID> == i_guildstone
		IF <LINK.UID> == <VAR.UID_GUILDE>
			RETURN 1
		ENDIF
	ENDIF
ENDFOR
RETURN 0